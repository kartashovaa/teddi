plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.0"
    id "java-gradle-plugin"
    id "maven-publish"
    id "signing"
}

group = "io.github.kartashovaa"
version = "0.0.3-SNAPSHOT"

def integrationSourceSet = sourceSets.create("integrationTest")
tasks.register("testIntegrationTest", Test) {
    testClassesDirs = integrationSourceSet.output
    classpath = integrationSourceSet.runtimeClasspath
    dependsOn(tasks.named("publishToMavenLocal"))
}

gradlePlugin {
    plugins {
        testDiff {
            id = "io.github.kartashovaa.teddi"
            implementationClass = "io.github.kartashovaa.teddi.TestDiffPlugin"
        }
    }

    testSourceSet(integrationSourceSet)
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:7.4.0'
    implementation 'org.ow2.asm:asm:9.5'

    testImplementation "junit:junit:4.13.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.1"
    integrationTestImplementation "junit:junit:4.13.2"
    integrationTestRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.1"
    integrationTestImplementation 'org.eclipse.jgit:org.eclipse.jgit:3.5.0.201409260305-r'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(11)
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        }
    }
    publications {
        teddi(MavenPublication) {
            group = "io.github.kartashovaa.teddi"
            artifactId = "io.github.kartashovaa.teddi.gradle.plugin"
            from components.java

            pom {
                name = "Teddi"
                description = "Gradle plugin for testing diff between commits"
                url = "https://github.com/kartashovaa/teddi"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "kartashovaa"
                        name = "Andrey Kartashov"
                        email = "kartashovandrey73@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/kartashovaa/teddi.git"
                    developerConnection = "scm:git:ssh://github.com:kartashovaa/teddi.git"
                    url = "https://github.com/kartashovaa/teddi"
                }
            }
        }
    }
}

signing {
    //TODO: find better way to disable sign for tests
    setRequired(gradle.taskGraph.hasTask("publishTeddiPublicationToMavenCentralRepository"))
    sign publishing.publications.teddi
}
